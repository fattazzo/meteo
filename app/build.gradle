apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    signingConfigs {
        config {
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.gmail.fattazzo.meteo"
        minSdkVersion 21
        targetSdkVersion 28
        multiDexEnabled true
        versionCode 41
        versionName "5.5"
        setProperty("archivesBaseName", "MeteoDelTrentino")
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
    testOptions {
        unitTests.returnDefaultValues = true
        animationsDisabled = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            def version = variant.versionName
            def date = new Date()
            def formattedDate = date.format('ddMMyy_hhmmss')
            outputFileName = "${archivesBaseName}-${version}_${formattedDate}.apk"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kapt {
        correctErrorTypes = true
    }
}

afterEvaluate {
    android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.startsWith("test"))
        {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}

dependencies {
    // Compile ---------------------------------------------------------------
    implementation 'com.android.support:multidex:1.0.3'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.1.0-beta01'
    implementation 'androidx.appcompat:appcompat:1.1.0-beta01'
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    implementation 'com.google.android.material:material:1.1.0-alpha07'

    kapt "org.androidannotations:androidannotations:$aa_version"
    implementation "org.androidannotations:androidannotations-api:$aa_version"

    implementation 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'

    implementation 'com.squareup.okio:okio:2.2.2'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.2'

    implementation 'com.squareup.okhttp3:okhttp:4.0.0-alpha02'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    implementation('com.squareup.retrofit2:converter-simplexml:2.6.0') {
        exclude module: 'stax-api'
        exclude module: 'stax'
        exclude module: 'xpp3'
    }

    implementation 'org.jsoup:jsoup:1.12.1'


    implementation 'com.github.vipulasri:timelineview:1.1.0'

    implementation 'com.google.code.gson:gson:2.8.5'

    implementation 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'

    implementation "com.github.fattazzo:android-about-library:0.5.2"
    // Test ------------------------------------------------------------------
    testImplementation 'junit:junit:4.13-beta-3'
    testImplementation 'org.mockito:mockito-core:2.28.2'

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    // Android test ----------------------------------------------------------------
    androidTestImplementation 'androidx.test:core:1.2.1-alpha01'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2-alpha01'
    androidTestImplementation('androidx.test:runner:1.3.0-alpha01') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation('androidx.test:rules:1.3.0-alpha01') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation('androidx.test.espresso:espresso-core:3.3.0-alpha01') {
        exclude module: 'support-annotations'
    }
    // Espresso-contrib for DatePicker, RecyclerView, Drawer actions, Accessibility checks, CountingIdlingResource
    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.3.0-alpha01') {
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
    }
}
repositories {
    mavenCentral()

    // ActiveAndroid
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    google()
}
